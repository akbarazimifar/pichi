# Force to use static linking for iOS
if (IOS)
  if (NOT STATIC_LINK)
    message(WARNING "Force to use static linking on iOS")
    set(STATIC_LINK ON)
  endif ()
  if (BUILD_SERVER OR BUILD_TEST)
    message(WARNING "Executable file is prohibited on iOS")
    set(BUILD_SERVER OFF)
    set(BUILD_TEST OFF)
  endif ()
  if ("armv7" IN_LIST ARCHS OR "armv7s" IN_LIST ARCHS)
    add_compile_options(-faligned-allocation)
  endif ()
endif ()

# Setting library suffix for linking
if (STATIC_LINK)
  if (UNIX)
    set(CMAKE_FIND_LIBRARY_SUFFIXES .a)
  else ()
    set(CMAKE_FIND_LIBRARY_SUFFIXES .lib)
  endif ()
endif ()

# Using rpath for dynamic linking
if (UNIX AND NOT STATIC_LINK)
  set(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/lib")
  set(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)
endif ()

set(BOOST_COMPONENTS context system)
if (WIN32 AND NOT STATIC_LINK)
  set(BOOST_COMPONENTS ${BOOST_COMPONENTS} date_time)
endif ()
if (BUILD_SERVER)
  set(BOOST_COMPONENTS ${BOOST_COMPONENTS} filesystem program_options)
endif ()
if (BUILD_TEST)
  set(BOOST_COMPONENTS ${BOOST_COMPONENTS} unit_test_framework)
endif ()

# C++ standard options
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
